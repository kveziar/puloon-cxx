cmake_minimum_required(VERSION 3.0)

cmake_policy(VERSION 3.0)

project(puloon-cxx VERSION 0.1.0 LANGUAGES CXX)

# project variables
set(PULOON_PACKAGE_NAME ${PROJECT_NAME})
set(PULOON_EXPORT_NAME ${PULOON_PACKAGE_NAME}-targets)
set(PULOON_CONFIG_FILENAME ${PULOON_PACKAGE_NAME}-config.cmake)
set(PULOON_CONFIGVERSION_FILENAME ${PULOON_PACKAGE_NAME}-config-version.cmake)
set(PULOON_CMAKE_DIR cmake)
set(PULOON_TARGET_NAME ${PROJECT_NAME})

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)

# set output directories for each build configuration
foreach(CONFIG ${CMAKE_CONFIGURATION_TYPES})
	string(TOUPPER ${CONFIG} UPPER_CONFIG)
	set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}/${CONFIG})
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/${CONFIG})
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CONFIG})
endforeach(CONFIG CMAKE_CONFIGURATION_TYPES)

add_subdirectory(src)

configure_file(
	${PULOON_CMAKE_DIR}/${PULOON_CONFIG_FILENAME}.in
	${PULOON_CMAKE_DIR}/${PULOON_CONFIG_FILENAME}
	@ONLY
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file(${PULOON_CMAKE_DIR}/${PULOON_CONFIGVERSION_FILENAME}
	COMPATIBILITY SameMajorVersion
)

export(EXPORT ${PULOON_EXPORT_NAME} FILE ${PULOON_CMAKE_DIR}/${PULOON_EXPORT_NAME}.cmake)
export(PACKAGE ${PULOON_PACKAGE_NAME})

if(WIN32)
	install(FILES
		${PROJECT_BINARY_DIR}/${PULOON_CMAKE_DIR}/${PULOON_CONFIG_FILENAME}
		${PROJECT_BINARY_DIR}/${PULOON_CMAKE_DIR}/${PULOON_CONFIGVERSION_FILENAME}
		DESTINATION ${PULOON_CMAKE_DIR}
	)
	install(EXPORT ${PULOON_EXPORT_NAME}
		DESTINATION ${PULOON_CMAKE_DIR}
	)
else(WIN32)
	install(FILES
		${PROJECT_BINARY_DIR}/${PULOON_CMAKE_DIR}/${PULOON_CONFIG_FILENAME}
		${PROJECT_BINARY_DIR}/${PULOON_CMAKE_DIR}/${PULOON_CONFIGVERSION_FILENAME}
		DESTINATION lib/${PULOON_CMAKE_DIR}/${PULOON_PACKAGE_NAME}
	)
	install(EXPORT ${PULOON_EXPORT_NAME}
		DESTINATION lib/${PULOON_CMAKE_DIR}/${PULOON_PACKAGE_NAME}
	)
endif(WIN32)

install(DIRECTORY
	include
	DESTINATION .
)
